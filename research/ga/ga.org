#+TITLE:  Learning Genetic Algorithms
#+AUTHOR: Lucas Vieira
#+EMAIL:  lucasvieira@lisp.com.br
#+DATE:   Dec 27, 2018
#+PROPERTY: header-args:lisp :cache yes :exports code :tangle yes
#+STARTUP:  showall

* Introduction

A teacher of mine tasked me with the challenge of building a genetic algorithm
to solve diophantine equations. Before I jump into it, I will first attempt to
build a simple genetic algorithm which just evolves in a very stupid manner. I
am using a certain tutorial[fn:1] for that.

The goal here is to have a minimal environment in which 32-bit numbers keep
evolving, until they reach twenty ~1~'s on their binary composition.

This GA is built using Common Lisp, due to my distaste for Java. If you're
allergic to parenthesis, you've been warned.

* Initial population

Population is stored in a list. Unfortunately, this means that checking if a
chromosome is in the list is ~O(n)~ speed at worst-case scenario (which is also a
recurrent thing). I'll leave optimization heuristics for later, though.

#+BEGIN_SRC lisp
(defparameter *population* nil)
(defparameter *fittest* nil)
(defparameter *max-fitness* 20)
(defparameter *rnd-state* (make-random-state))
#+END_SRC

#+RESULTS[ee1f53f217a5bf9fb606fe0af8f94fd52cdba31a]:
: *RND-STATE*

Each individual on ~*population*~ is a different chromosome. The genes
themselves will be the bits of each number. The two numbers of large fitness are
stored in ~*fittest*~. ~*max-fitness*~ tells us the least number of ~1~'s we're
expecting. And then, we have an initial ~*rnd-state*~ for randomization purposes.

** Zygotes: partial chromosomes

Having a 32-bit number as chromosome also means that we need to look at the 16
least significant bits at crossover phase, but the 16 most significant bits can
be randomly generated. For that, we use the global random state which will
provide our random numbers.

#+BEGIN_SRC lisp
(defun make-zygote ()
  (let ((chromosome 0))
    (declare ((unsigned-byte 32) chromosome))
    (dotimes (i 16)
      ;; Run d20 < 5 to see if bit should be set
      (setf chromosome (logior (ash chromosome 1)
			       (if (< (random 20 *rnd-state*) 5) 1 0))))
    (ash chromosome 16)))
#+END_SRC

#+RESULTS[d403dc4036eacef73358290cef69c1f6a212e3dd]:
: MAKE-ZYGOTE

I'm calling here a chromosome in a not-ready state a /zygote/, since it is still
expecting a crossover phase. Each zygote gene has a chance of 3/10 (or 6/20) to
be a ~1~.

Printing any chromosome, be it a zygote or not, is rather easy. We just instruct
the ~format~ function to pad it with 32 positions, and fill the nonexistant
leading ones with zeroes. We can even build a function for that:

#+BEGIN_SRC lisp
(defun format-chromosome (chromosome)
  (declare ((unsigned-byte 32) chromosome))
  (format nil "~32,'0b" chromosome))

(defun print-chromosome (chromosome)
  (princ (format-chromosome chromosome))
  (terpri))
#+END_SRC

#+RESULTS[85ac28687cf4be2496afb147f4050506edad74d7]:
: PRINT-CHROMOSOME

*** Example: Checking zygote generation

Here is an example of what happens when you print an immediately created
zygote. The first line is the actual value of the 32-bit integer, and the second
line is the binary representation of such integer, when you call
~print-chromosome~ using its value.

#+BEGIN_SRC lisp :tangle no :results output :exports results
(let ((zygote (make-zygote)))
  (princ zygote)
  (terpri)
  (print-chromosome zygote))
#+END_SRC

#+RESULTS[2df934f9717138064c89f6937ef3008e27586f2a]:
: 138412032
: 00001000010000000000000000000000



** Generating initial chromosomes

We generate our first ten chromosomes in a very simple way. We just generate
them from zygotes and put them on the list. We also use a helper macro which
will only push a new chromosome onto the list if it is not already there.

#+BEGIN_SRC lisp
(defparameter *distinction-threshold*
  (truncate (* (expt 2 32) 0.0001)))

(defmacro with-distinct-chromosome (&body generator-thunk)
  (let ((chr-sym (gensym))
	(repets  (gensym)))
    `(let ((,chr-sym nil)
	   (,repets 0))
       ;; Only perform operation if population has not converged
       (when (null *convergence-flag*)
	 ;; Keep repeating until convergence, or until new
	 ;; chromosome is found
	 (loop while (or (eq ,chr-sym nil)
			 (member ,chr-sym *population*))
	    do (setf ,chr-sym (progn ,@generator-thunk)
		     ,repets  (1+ ,repets))
	    never (>= ,repets *distinction-threshold*))
	 (unless (>= ,repets *distinction-threshold*)
	   ;; If generation attempts have not been exhausted,
	   ;; go ahead and push those individuals to population
	     (let ((fitness (calc-fitness ,chr-sym)))
	       (push ,chr-sym *population*)
	       ;; Also see if their fitness is enough to make
	       ;; them fittest individuals
	       (cond ((or (null *fittest*)
			  (< (length *fittest*) 2))
		      (push ,chr-sym *fittest*))
		     ((> fitness (calc-fitness (car *fittest*)))
		      (setf *fittest* (list ,chr-sym (car *fittest*))))
		     ((> fitness (calc-fitness (cadr *fittest*)))
		      (setf *fittest* (list (car *fittest*) ,chr-sym))))))))))

(defun make-chromosome ()
  (logior (make-zygote)
	  (ash 1 (random 16 *rnd-state*))))

(defun initialize-population ()
  (setf *population*       nil
	,*fittest*          nil
	,*convergence-flag* nil)
  (dotimes (i 10)
    (with-distinct-chromosome (make-chromosome))))

#+END_SRC

#+RESULTS[6008503f841c729cb08101fd80d6339d8a0a6911]:
: INITIALIZE-POPULATION

The macro ~with-distinct-chromosome~ takes a generator thunk and performs it over
and over again, until the generated chromosome is not a member of the population
anymore. Since this behaviour may cause an infinite loop (or a seemingly
infinite loop in case we get stuck for a while), we define a distinction
threshold for this repetition.

In case we end up stuck in what could be an infinite loop, our macro halts the
chromosome generation, and gives up on adding the new chromosome to the
population. Since this macro is so useful on making sure we are not testing
again any chromosomes which we've tested on the near past, we also use it later,
on the crossover phase.

It is deduced that the chromosome generation is in an infinite loop if the
repetition was performed for roughly a number of 0.01% of the distinct
chromosomes we could generate (about 429496 numbers). We do not make
distinctions between different numbers to count the amount of repetitions,
though.

The macro also compares the fitness of the newly-generated number. We always
need to have at least two fittest chromosomes. For that, when we identify a
chromosome which could be added to the list, we compare if its fitness is
greater than one of the two most-fit chromosomes. If that is true, said
chromosome will be replaced, and the newly generated chromosome will occupy its
proper space. Fittest chromosomes are arranged in such a way that the fittest of
the couple remains on top. More information about calculating fitness will be
given later. This represents a phase called *selection*; more on it will be said
later.

The ~initialize~ function makes good use of said macro, by generating ten distinct
chromosomes: it takes a zygote, then sets a random flag at one of the 16 least
significant bits to 1, ensuring that we'll most likely have ten different
bitmasks at the beginning.

* Computing fitness

Our fitness is calculated by the amount of ~1~'s in the binary representation of
our number. The more the amount, the better.

We define two functions: one is a predicate which, given the index of a bit,
returns ~t~ if the bit is set to ~1~. The other one loops through every possible bit
and counts how many of them are ~1~'s.

#+BEGIN_SRC lisp
(defun bit-set-p (bitmask bit-index)
  (declare ((unsigned-byte 32) bitmask)
	   ((unsigned-byte 8) bit-index))
  (if (<= bit-index 31)
      (not (= (logand bitmask (ash 1 bit-index)) 0))
      nil))

(defun calc-fitness (chromosome)
  (loop for x below 32
     when (bit-set-p chromosome x)
     sum 1))
#+END_SRC

#+RESULTS[fc867b0dcbc1bcbfdd8d56d5709379ba2bb05bca]:
: CALC-FITNESS

We also add a convenient function for printing the chromosome with its fitness.

#+BEGIN_SRC lisp
(defun print-chromosome-with-fitness (chrm)
  (format t "~a => ~a~&"
	  (format-chromosome chrm)
	  (calc-fitness chrm)))
#+END_SRC

#+RESULTS[4c05e490073addb2bbb68a1baadc6dd4fe3e63a6]:
: PRINT-CHROMOSOME-WITH-FITNESS

** Example: Checking generated population

We can use ~mapcan~ along with a special anonymous function over the population to
beautifully print all of our generated specimen, and each chromosome's
fitness. Here is an example.

#+BEGIN_SRC lisp :tangle no :results output :exports both
;; Initialize a population first
(initialize-population)

(mapcan (lambda (chrm)
	  (format t "~a => ~a~%"
		  (format-chromosome chrm)
		  (calc-fitness chrm)))
	,*population*)
#+END_SRC

#+RESULTS[6624a2f4b7380b978cf04bb8dbe55ebc992ba2e7]:
#+begin_example
00100000000101000001000000000000 => 4
00000011000001010000001000000000 => 5
01011001010000000000000010000000 => 6
01111001000000000000000000000100 => 6
00011010000000000000000000100000 => 4
01000000010010100000000000001000 => 5
00010100100000000000001000000000 => 4
00000011000000000000000010000000 => 3
10011010010000000010000000000000 => 6
00100001000000100100000000000000 => 4
#+end_example

* Selection

The selection phase is where the two fittest individuals are selected to pass on
their genes to the next generation. This phase is automagically done on the
~with-distinct-chromosome~ macro.

** Example: Checking selected chromosomes

Using the same principle of printing the population, we can do the same with
~*fittest*~, which is the variable storing the two fittest individuals. This time,
though, their fitness values are irrelevant, so passing ~print-chromosome~ over
each one with the aid of ~mapcan~ should do the trick.

#+BEGIN_SRC lisp :tangle no :results output :exports both
(mapcan #'print-chromosome *fittest*)
#+END_SRC

#+RESULTS[673ff0d68926dda63c601f59644b785ac5e0b6dc]:
: 01111110111111010000100110010111
: 01101110111110010000100010111100

* Crossover (Breeding)

The crossover process produces two new chromosomes. We generate a new individual
by making the parents exchange their least significant bits. Each new value is,
then, added to the population, and the selection phase takes place.

#+BEGIN_SRC lisp
(defun breed (parent1 parent2)
  (declare ((unsigned-byte 32) parent1 parent2))
  (labels ((swap-bit (n)
	     (let ((bit-p1 (bit-set-p parent1 n))
		   (bit-p2 (bit-set-p parent2 n)))
	       (when (not (eq bit-p1 bit-p2))
		 (setf parent1 (logxor parent1 (ash 1 n))
		       parent2 (logxor parent2 (ash 1 n)))))))
    (dotimes (i 16)
      (swap-bit i)))
  (values parent1 parent2))
#+END_SRC

#+RESULTS[08e178012598e489997237f9c71ede10a0b6e19c]:
: BREED

We also define a function to ensure our population never surpasses its fixed
size. If it does, then the individuals of least fitness are removed.

#+BEGIN_SRC lisp
(defun limit-population (max-num)
  (let* ((pop-len (length *population*))
	 (exceed (- pop-len max-num)))
    (when (> exceed 0)
      (let ((pop-fitness (mapcar #'calc-fitness *population*)))
	(labels ((remove-minimum ()
		   (let ((min-index
			  (loop for i below pop-len
			     for elt in pop-fitness
			     with min = (cons 32 nil)
			     when (< elt (car min))
			     do (setf min (cons elt i))
			     finally (return (cdr min)))))
		     (when min-index
		       (setf *population*
			     (loop for elt in *population*
				for i from 0
				unless (= i min-index) collect elt)
			     pop-len (1- pop-len))))))
	  (dotimes (i exceed)
	    (remove-minimum)))))))
#+END_SRC

#+RESULTS[1600186dc53f31ec041946993fd7bdd3f250e592]:
: LIMIT-POPULATION

* Mutation

To ensure population diversity and remove the chances of early convergence, we
mutate some genes at a low rate. By picking a maximum of 16 genes, regardless of
significancy, at random, it is possible to roll the dice again (with little
probability): should the odds be on favor, the specified gene will suffer a
"flip".

#+BEGIN_SRC lisp
(defun mutate (chromosome)
  (declare ((unsigned-byte 32) chromosome))
  (let* ((num-mutations (random 16 *rnd-state*))
	 (mutating-indexes (remove-duplicates
			    (loop for i below num-mutations
				collect (random 32 *rnd-state*)))))
    (labels ((attempt-mutation-at (n)
	       ;; Throw d20 and expect < 8
	       (when (< (random 20 *rnd-state*) 7)
		 (setf chromosome (logxor chromosome (ash 1 n))))))
      (mapcan #'attempt-mutation-at mutating-indexes)))
  chromosome)
#+END_SRC

#+RESULTS[6dbd53c23b865c6ca78a0a3ec9bd11c854dd038d]:
: MUTATE

** Example: Testing the mutation algorithm

We can check whether this is working or not with a simple algorithm which
generates a chromosome and its mutation; then, we just print them and see if
they can be compared. By re-running it a couple of times, we should see a
mutated gene here and there.

#+BEGIN_SRC lisp :tangle no :results output :exports both
(let ((chromosome (make-chromosome)))
  (mapcan #'print-chromosome
	  (list chromosome
		(mutate chromosome))))
#+END_SRC

#+RESULTS[8f77e6d1801cdd7607aecab2e5a9c4d720b560b7]:
: 00101001000010000000000000100000
: 00101101000011000100000000100000

* Crossover (Finalization)

Now we define our actual crossover function. The crossover function takes into
consideration both the breeding process and the mutation.

#+BEGIN_SRC lisp
(defun crossover ()
  (multiple-value-bind (offspring1 offspring2)
      (apply #'breed *fittest*)
    (with-distinct-chromosome (mutate offspring1))
    (with-distinct-chromosome (mutate offspring2))))
#+END_SRC

#+RESULTS[b630460668c6ffca75b2c6cd585ec214c48be816]:
: CROSSOVER

* Convergence

We need a strategy to check if our population converged. For a genetic
algorithm, a converged population usually means that we've reached a max
fitness. Therefore, we just need to check if our fittest individual has a
fitness greater or equal than the specified max fitness.

#+BEGIN_SRC lisp
(defun population-converged-p ()
  (>= (calc-fitness (car *fittest*))
      ,*max-fitness*))
#+END_SRC

#+RESULTS[e1d47d93bbb55d7027344fe2d40bc60a8b1201e4]:
: POPULATION-CONVERGED-P

* Debriefing and running the genetic algorithm

Now we can create a proper loop which will execute the steps of our algorithm
until the population converges.

#+BEGIN_SRC lisp
(defun debriefing ()
  (princ "Population converged.") (terpri)
  (format t "Best fitness: ~a~%~%" (calc-fitness (car *fittest*)))
  (princ "Final population:") (terpri)
  (mapcan #'print-chromosome-with-fitness *population*)
  (terpri))

(defun run-genetic-algorithm ()
  (initialize-population)
   (loop until (population-converged-p)
      for i from 0
      do (format t "Generation: ~a~%Fittest:~%" i)
	(mapcan #'print-chromosome *fittest*)
	(terpri)
	(crossover)
	(limit-population 10))
  (debriefing))
#+END_SRC

#+RESULTS[1874d4fce158597dec9d1377c491f12d7789f139]:
: RUN-GENETIC-ALGORITHM

* Conclusion

With everything all sorted and done, we can test our algorithm at once. Here's
one possible output when running the function ~run-genetic-algorithm~:

#+BEGIN_SRC lisp :tangle no :exports results :results output
(run-genetic-algorithm)
#+END_SRC

#+RESULTS[32db23317bf915961623d386c29ac67e9b73aca1]:
#+begin_example
Generation: 0
Fittest:
00100110100010010010000000000000
00010010100001010001000000000000

Generation: 1
Fittest:
00100110100010010001000010000010
00100110100010010010000000000000

Generation: 2
Fittest:
00100110100010010010001001010000
00100110100010010001000010000010

Generation: 3
Fittest:
00101110101010010001000110010010
00100110100010010010001001010100

Generation: 4
Fittest:
00110110100010010000100110110110
00101110101010010001000110010010

Generation: 5
Fittest:
00110110100010010000100110110110
00101110101010010000100110010110

Generation: 6
Fittest:
01101110111110010000100010111100
00110110100010010000100110110110

Population converged.
Best fitness: 20

Final population:
00110110100010010000100010111100 => 13
01111110111111010000100110010111 => 20
01101110111110010000100010111100 => 17
00110110100010010010100110010110 => 14
00101110101010010000100110010110 => 14
00110110100010010001000010010010 => 11
00110110100010010000100110110110 => 14
00101110101010010010001001010100 => 13
00100110100010010010001001010100 => 11
00101110101010010001000110010010 => 13

#+end_example

* Footnotes

[fn:1] [[https://towardsdatascience.com/introduction-to-genetic-algorithms-including-example-code-e396e98d8bf3][Introduction to Genetic Algorithms — Including Example Code]]
